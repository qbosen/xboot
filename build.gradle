buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:$spotbugsVersion"
        classpath "org.owasp:dependency-check-gradle:$dependencyCheckVersion"
    }
}
plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "org.springframework.boot" version "$springbootVersion"
    id "io.spring.dependency-management" version "$springDependencyMgrVersion"

}

repositories {
    maven { url "http://192.168.6.120:8181/repository/maven-public/" }
    maven { url "https://maven.aliyun.com/repository/central" }
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    api("top.abosen.dddboot:dddboot-shared-model:0.0.1-SNAPSHOT") { changing = true }
    api 'jakarta.servlet:jakarta.servlet-api'
    api 'jakarta.persistence:jakarta.persistence-api'
    api 'jakarta.validation:jakarta.validation-api'
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.data:spring-data-commons'

    api "org.springdoc:springdoc-openapi-ui:$springDocVersion"
    api('org.springframework.boot:spring-boot-starter-web')

    implementation "com.baomidou:mybatis-plus:$mybatisPlusVersion"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"


    // lombok dependencies
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    //test dependencies
    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testImplementation("org.mockito:mockito-junit-jupiter:$mockitoJunitVersion")

}

jar {
    enabled = true
}
bootJar {
    enabled = false
}


configurations.all {
    exclude group: "junit", module: "junit"
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.artifact
            version = rootProject.version
            from components.java
        }
    }

    repositories {
        maven {
            name '120'
            credentials { username "deployment"; password "123456" }
            url = version.endsWith('SNAPSHOT') ?
                    "http://192.168.6.120:8181/repository/maven-snapshots/" :
                    "http://192.168.6.120:8181/repository/maven-releases/"
        }
    }
}

test {
    useJUnitPlatform()
}

apply from: "$projectDir/gradle/checkstyle/checkstyle.gradle"
apply from: "$projectDir/gradle/spotbugs/spotbugs.gradle"
apply from: "$projectDir/gradle/git-hooks/git-hooks.gradle"
apply from: "$projectDir/gradle/dependency-check/dependency-check.gradle"
apply from: "$projectDir/gradle/version-info.gradle"
apply from: "$projectDir/gradle/jacoco.gradle"
