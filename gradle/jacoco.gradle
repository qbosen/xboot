apply plugin: "jacoco"

ext {
    jacocoLineMin = 0
    jacocoBranchMin = 0
    jacocoToolVersion = "0.8.6"
    // 配置需要忽略的文件
    jacocoExcludes = [
    ]
}

jacoco {
    toolVersion = jacocoToolVersion
}

//Unit test report
jacocoTestReport {
    shouldRunAfter test
    reports {
        html.enabled true
        xml.enabled false
        csv.enabled false
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        })
    }
}
// https://www.jacoco.org/jacoco/trunk/doc/index.html
//Unit test code coverage
jacocoTestCoverageVerification {
    shouldRunAfter test
    executionData test
    violationRules {
        rule {
            element = "BUNDLE"
            limit {
                counter = "INSTRUCTION"
                minimum = jacocoLineMin
            }
        }

        rule {
            element = "BUNDLE"
            limit {
                counter = "BRANCH"
                minimum = jacocoBranchMin
            }
        }
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        })
    }
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
// 不支持增量式构件
//check.dependsOn jacocoTestReport
//check.dependsOn jacocoTestCoverageVerification
